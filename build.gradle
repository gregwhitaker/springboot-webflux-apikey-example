plugins {
    id "idea"
    id "java"
    id "com.github.hierynomus.license"      version "0.14.0"
    id "org.flywaydb.flyway"                version "6.4.4"
    id "org.springframework.boot"           version "2.3.6.RELEASE"
    id "io.spring.dependency-management"    version "1.0.10.RELEASE"
}

// Project Configuration
group   = "com.github.gregwhitaker"
version = "0.1.0"

// Dependency Management
repositories {
    mavenCentral()
}

dependencies {
    implementation "com.github.ben-manes.caffeine:caffeine:2.6.2"
    implementation "commons-codec:commons-codec:1.11"
    implementation "org.flywaydb:flyway-core"
    implementation "org.springframework.boot:spring-boot-starter-jdbc"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-webflux"

    runtime "org.postgresql:postgresql"

    testImplementation "io.projectreactor:reactor-test"
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation "org.springframework.security:spring-security-test"
}

// Test
test {
    useJUnitPlatform()
}

// Docker
bootBuildImage {
    verboseLogging = true
    imageName = "${project.group}/${project.name}:${project.version}"
}

// Database
flyway {
    url = "jdbc:postgresql://localhost:5432/postgres"
    user = "postgres"
}

// Licensing
license {
    header rootProject.file("codequality/HEADER")
    strictCheck true
    excludes([ "**/*.json", "**/*.html", "**/*.js", "**/*.jpg" ])
}
